/*
 * Copyright (c) 2017 Zac Sweers
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.library'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'org.jetbrains.kotlin.kapt'

android {
  compileSdkVersion deps.android.build.compileSdkVersion
  buildToolsVersion deps.android.build.buildToolsVersion

  defaultConfig {
    minSdkVersion deps.android.build.minSdkVersion
    targetSdkVersion deps.android.build.targetSdkVersion
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
    debug.java.srcDirs += 'src/debug/kotlin'
    release.java.srcDirs += 'src/release/kotlin'
    test.java.srcDirs += 'src/test/kotlin'

    // For kapt
    main.java.srcDirs += [file("$buildDir/generated/source/kapt/main")]
    debug.java.srcDirs += [file("$buildDir/generated/source/kapt/debug")]
    release.java.srcDirs += [file("$buildDir/generated/source/kapt/release")]
    test.java.srcDirs += [file("$buildDir/generated/source/kapt/test")]
  }
  lintOptions {
    lintConfig file("lint.xml")
    abortOnError true
    check 'NewApi', 'InlinedApi'
    fatal 'NewApi', 'InlinedApi'
    enable 'UnusedResources'
    checkReleaseBuilds true
    textReport deps.build.ci
    textOutput 'stdout'
    htmlReport !deps.build.ci
    xmlReport !deps.build.ci
  }
}

kapt {
  correctErrorTypes = true
}

configurations.all {
  resolutionStrategy.eachDependency { DependencyResolveDetails details ->
    if (details.requested.group == 'com.android.support' &&
        !details.requested.name.contains('multidex')) {
      // We want to force all support libraries to use the same version, even if they're transitive.
      details.useVersion versions.support
    }
    if (details.requested.group == 'org.jetbrains.kotlin') {
      // We want to force all kotlin libraries to use the same version, even if they're transitive.
      details.useVersion versions.kotlin
    }
  }
}

dependencies {
  implementation project(':service-api')
  implementation project(':util')

  kapt deps.dagger.apt.compiler
  kapt deps.tikxml.apt

  implementation deps.tikxml.annotation
  implementation deps.tikxml.core
  api deps.tikxml.htmlEscape
  implementation deps.tikxml.retrofit
  implementation deps.okhttp.core
  implementation deps.retrofit.core
  implementation deps.retrofit.rxJava2

  api deps.android.support.annotations
  api deps.dagger.runtime
  api deps.misc.lazythreeten
  api deps.rx.java2
}
