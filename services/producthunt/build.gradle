/*
 * Copyright (c) 2017 Zac Sweers
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.library'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'org.jetbrains.kotlin.kapt'

private String getPropertyOrEmpty(String property) {
  if (hasProperty(property)) {
    return project.properties[property]
  } else {
    return ""
  }
}

android {
  compileSdkVersion deps.android.build.compileSdkVersion
  buildToolsVersion deps.android.build.buildToolsVersion

  defaultConfig {
    minSdkVersion deps.android.build.minSdkVersion
    targetSdkVersion deps.android.build.targetSdkVersion
    buildConfigField 'String', 'PROCUCT_HUNT_DEVELOPER_TOKEN',
        /"${getPropertyOrEmpty("catchup_product_hunt_developer_token")}"/
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
    debug.java.srcDirs += 'src/debug/kotlin'
    release.java.srcDirs += 'src/release/kotlin'
    test.java.srcDirs += 'src/test/kotlin'

    // For kapt
    main.java.srcDirs += [file("$buildDir/generated/source/kapt/main")]
    debug.java.srcDirs += [file("$buildDir/generated/source/kapt/debug")]
    release.java.srcDirs += [file("$buildDir/generated/source/kapt/release")]
    test.java.srcDirs += [file("$buildDir/generated/source/kapt/test")]
  }
  lintOptions {
    lintConfig file("lint.xml")
    abortOnError true
    check 'NewApi', 'InlinedApi'
    fatal 'NewApi', 'InlinedApi'
    enable 'UnusedResources'
    checkReleaseBuilds true
    textReport deps.build.ci
    textOutput 'stdout'
    htmlReport !deps.build.ci
    xmlReport !deps.build.ci
  }
}

kapt {
  correctErrorTypes = true
}

dependencies {
  api project(':service-api')
  implementation project(':util')

  compileOnly deps.auto.value.compileOnly.annotations
  compileOnly deps.auto.value.extensions.moshi.compileOnly.annotations
  kapt deps.dagger.apt.compiler
  kapt deps.auto.value.apt.core
  kapt deps.auto.value.extensions.moshi.apt.core
  kapt deps.auto.value.extensions.redacted.apt.core

  implementation deps.misc.okio
  implementation deps.misc.moshi
  implementation deps.retrofit.core
  implementation deps.retrofit.moshi
  implementation deps.retrofit.rxJava2
  implementation deps.okhttp.core
  implementation deps.misc.moshiLazyAdapters

  api deps.android.support.annotations
  api deps.dagger.runtime
  api deps.misc.lazythreeten
  api deps.rx.java2
}
