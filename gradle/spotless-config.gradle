/*
 * Copyright (c) 2019 Zac Sweers
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.diffplug.gradle.spotless'
String[] copiedJavaFiles = [
    "AtomicThrowable",
    "AutoDisposableHelper",
    "AutoDisposeBackpressureHelper",
    "AutoDisposeEndConsumerHelper",
    "AutoSubscriptionHelper",
    "ExceptionHelper",
    "HalfSerializer",
    "copyright",
].collect { "**/*${it}.java" } as String[]
String[] copiedKotlinFiles = [
    "dependencies",
    "copyright",
    "WeakHashSet",
    "KotterKnife"
].collect { "**/*${it}.kt" } as String[]
spotless {
  format 'misc', {
    target '**/*.md', '**/.gitignore'
    targetExclude "**/.gradle/**"
    indentWithTabs()
    trimTrailingWhitespace()
    endWithNewline()
  }
  kotlin {
    target "**/*.kt", "**/*.kt"
    targetExclude(copiedKotlinFiles)
    ktlint("0.31.0").userData(['indent_size': '2', 'continuation_indent_size' : '2'])
    licenseHeaderFile rootProject.file('spotless/copyright.kt')
    trimTrailingWhitespace()
    endWithNewline()
  }
  kotlinGradle {
    target '*.gradle.kts'
    ktlint("0.31.0").userData(['indent_size': '2', 'continuation_indent_size' : '2'])
    trimTrailingWhitespace()
    endWithNewline()
  }
  java {
    target "**/*.java"
    targetExclude(copiedJavaFiles)
    googleJavaFormat("1.7")
    licenseHeaderFile rootProject.file('spotless/copyright.java')
    removeUnusedImports()
    trimTrailingWhitespace()
    endWithNewline()
  }
  groovyGradle {
    target '**/*.gradle'
    trimTrailingWhitespace()
    endWithNewline()
  }
}
