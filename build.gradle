/*
 * Copyright (c) 2017 Zac Sweers
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id 'com.gradle.build-scan' version '1.10.1'
}

buildScan {
  licenseAgreementUrl = 'https://gradle.com/terms-of-service'
  licenseAgree = 'yes'
}

def dependenciesFile = file('gradle/dependencies.gradle')
subprojects {
  apply from: dependenciesFile
  buildscript {
    repositories {
      google()
      jcenter()
      maven { url deps.build.repositories.kotlin12EAPSnapshots }
      maven { url deps.build.repositories.plugins }
      maven { url deps.build.repositories.snapshots }
    }

    dependencies {
      // Seemingly random other classpath dependencies are because of a gradle bug when sharing
      // buildscript deps. Oddly, not only do all of these need to be here to match :app, but they
      // need to also be in the same order.
      classpath deps.android.gradlePlugin
      classpath deps.kotlin.gradlePlugin
      classpath deps.android.firebase.gradlePlugin
      classpath deps.build.gradlePlugins.bugsnag
      classpath deps.build.gradlePlugins.psync
      classpath deps.build.gradlePlugins.versions
      classpath deps.errorProne.gradlePlugin
      classpath deps.apollo.gradlePlugin
      classpath deps.build.gradlePlugins.playPublisher
    }
  }

  repositories {
    google()
    jcenter()
    maven { url deps.build.repositories.kotlin12EAPSnapshots }
    maven { url deps.build.repositories.jitpack }
    maven { url deps.build.repositories.snapshots }
    maven { url 'https://oss.jfrog.org/libs-snapshot' }
  }

  configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
      if (details.requested.group == 'com.android.support' &&
          !details.requested.name.contains('multidex')) {
        // We want to force all support libraries to use the same version, even if they're transitive.
        details.useVersion versions.support
      }
      if (details.requested.group == 'com.google.android.gms') {
        // We want to force all play services libraries to use the same version, even if they're transitive.
        details.useVersion versions.playServices
      }
      if (details.requested.group == 'com.google.firebase') {
        // We want to force all play services libraries to use the same version, even if they're transitive.
        details.useVersion versions.firebase
      }
      if (details.requested.group == 'org.jetbrains.kotlin') {
        // We want to force all kotlin libraries to use the same version, even if they're transitive.
        details.useVersion versions.kotlin
      }
    }
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '4.3.1'
  distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

task clean(type: Delete) {
  delete rootProject.buildDir
}

apply from: 'gradle/dependencies.gradle'
