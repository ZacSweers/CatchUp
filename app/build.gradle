/*
 * Copyright (c) 2017 Zac Sweers
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
  repositories {
    jcenter()
    maven { url "https://maven.google.com" }
  }

  dependencies {
    classpath deps.android.gradlePlugin
    classpath deps.kotlin.gradlePlugin
    classpath deps.build.gradlePlugins.bugsnag
    classpath deps.build.gradlePlugins.psync
    classpath deps.build.gradlePlugins.versions
    classpath deps.errorProne.gradlePlugin
    classpath deps.apollo.gradlePlugin
  }
}

apply plugin: 'com.android.application'
apply plugin: 'io.sweers.psync'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'org.jetbrains.kotlin.kapt'
apply plugin: "net.ltgt.errorprone"
apply plugin: 'com.github.ben-manes.versions' // ./gradlew dependencyUpdates -Drevision=release
apply plugin: 'com.apollographql.android'
//apply plugin: 'com.bugsnag.android.gradle'

private String getPropertyOrEmpty(String property) {
  if (hasProperty(property)) {
    return project.properties[property]
  } else {
    return ""
  }
}

repositories {
  jcenter()
  maven { url "https://jitpack.io" }
  maven { url "https://maven.google.com" }
}

android {
  compileSdkVersion deps.android.build.compileSdkVersion
  buildToolsVersion deps.android.build.buildToolsVersion

  defaultConfig {
    applicationId "io.sweers.catchup"
    minSdkVersion deps.android.build.minSdkVersion
    targetSdkVersion deps.android.build.targetSdkVersion
    versionCode deps.build.gitCommitCount
    versionName deps.build.gitTag
    multiDexEnabled true
    archivesBaseName = "catchup"
    vectorDrawables.useSupportLibrary = true

    buildConfigField 'String', 'GIT_SHA', /"$deps.build.gitSha"/
    buildConfigField 'long', 'GIT_TIMESTAMP', "$deps.build.gitTimestamp"
    buildConfigField 'String', 'PROCUCT_HUNT_DEVELOPER_TOKEN',
        /"${getPropertyOrEmpty("catchup_product_hunt_developer_token")}"/
    buildConfigField 'String', 'GITHUB_DEVELOPER_TOKEN',
        /"${getPropertyOrEmpty("catchup_github_developer_token")}"/
    buildConfigField 'String', 'DRIBBBLE_CLIENT_ACCESS_TOKEN',
        /"${getPropertyOrEmpty("catchup_dribbble_access_token")}"/
    buildConfigField 'String', 'SMMRY_API_KEY', /"${getPropertyOrEmpty("catchup_smmry_api_key")}"/
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  signingConfigs {
    if (getPropertyOrEmpty("catchup_signing_location")) {
      release {
        storeFile file(getPropertyOrEmpty("catchup_signing_location"))
        keyAlias getPropertyOrEmpty("catchup_signing_alias")
        storePassword getPropertyOrEmpty("catchup_signing_store_password")
        keyPassword getPropertyOrEmpty("catchup_signing_key_password")
        v2SigningEnabled true
      }
    } else {
      release {
        storeFile file("placeholder")
        keyAlias "placeholder"
        storePassword "placeholder"
        keyPassword "placeholder"
        v2SigningEnabled true
      }
    }
  }
  sourceSets {
    // For kapt
    main.java.srcDirs += [file("$buildDir/generated/source/kapt/main")]
  }
  packagingOptions {
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/NOTICE'
    exclude 'LICENSE.txt'
    exclude 'META-INF/rxjava.properties'
    exclude 'META-INF/NOTICE.txt'
    exclude 'META-INF/LICENSE.txt'
    exclude 'META-INF/services/javax.annotation.processing.Processor'
  }
  buildTypes {
    debug {
      applicationIdSuffix ".debug"
      versionNameSuffix "-dev"
    }
    release {
      buildConfigField 'String', 'BUGSNAG_KEY', /"${getPropertyOrEmpty("catchup_bugsnag_key")}"/
      signingConfig signingConfigs.release
      postprocessing.proguardFiles =
          [getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro']
      postprocessing.optimizeCode = true
      postprocessing.obfuscate = false
    }
  }
  dexOptions {
    javaMaxHeapSize "2g"
  }
  lintOptions {
    lintConfig file("lint.xml")
    abortOnError true
    check 'NewApi', 'InlinedApi'
    fatal 'NewApi', 'InlinedApi'
    enable 'UnusedResources'
    checkReleaseBuilds true
    textReport deps.build.ci
    textOutput 'stdout'
    htmlReport !deps.build.ci
    xmlReport !deps.build.ci
  }
  applicationVariants.all {
    // Configure firebase
    String apiKey = null
    String appId = null
    String databaseUrl = null
    String gaTrackingId = null
    String gcmDefaultSender = null
    String storageBucket = null
    String defaultWebClientId = null
    String crashReportingApiKey = null
    String projectId = null
    if (it.buildType.name == "debug") {
      apiKey = project.properties.get('catchup.google_api_key.debug')
      appId = project.properties.get('catchup.google_app_id.debug')
      databaseUrl = project.properties.get('catchup.firebase_database_url.debug')
      gaTrackingId = project.properties.get('catchup.ga_trackingId.debug')
      gcmDefaultSender = project.properties.get('catchup.gcm_defaultSenderId.debug')
      storageBucket = project.properties.get('catchup.google_storage_bucket.debug')
      defaultWebClientId = project.properties.get('catchup.default_web_client_id.debug')
      crashReportingApiKey = project.properties.get('catchup.google_crash_reporting_api_key.debug')
      projectId = 'catchup.project_id.debug'
    } else if (it.buildType.name == "release") {
      apiKey = project.properties.get('catchup.google_api_key')
      appId = project.properties.get('catchup.google_app_id')
      databaseUrl = project.properties.get('catchup.firebase_database_url')
      gaTrackingId = project.properties.get('catchup.ga_trackingId')
      gcmDefaultSender = project.properties.get('catchup.gcm_defaultSenderId')
      storageBucket = project.properties.get('catchup.google_storage_bucket')
      defaultWebClientId = project.properties.get('catchup.default_web_client_id')
      crashReportingApiKey = project.properties.get('catchup.google_crash_reporting_api_key')
      projectId = project.properties.get('catchup.project_id')
    }
    if (apiKey) {
      it.resValue 'string', 'google_api_key', apiKey
    }
    if (appId) {
      it.resValue 'string', 'google_app_id', appId
    }
    if (databaseUrl) {
      it.resValue 'string', 'firebase_database_url', databaseUrl
    }
    if (gaTrackingId) {
      it.resValue 'string', 'ga_trackingId', gaTrackingId
    }
    if (gcmDefaultSender) {
      it.resValue 'string', 'gcm_defaultSenderId', gcmDefaultSender
    }
    if (storageBucket) {
      it.resValue 'string', 'google_storage_bucket', storageBucket
    }
    if (defaultWebClientId) {
      it.resValue 'string', 'default_web_client_id', defaultWebClientId
    }
    if (crashReportingApiKey) {
      it.resValue 'string', 'google_crash_reporting_api_key', crashReportingApiKey
    }
    if (projectId) {
      it.resValue 'string', 'project_id', projectId
    }
  }
}

//if (getPropertyOrEmpty("catchup_bugsnag_key")) {
//  bugsnag {
//    apiKey getPropertyOrEmpty("catchup_bugsnag_key")
//  }
//}

psync {
  includesPattern = "**/xml/prefs_*.xml"
  generateRx = true
  packageName = "io.sweers.catchup"
}

apollo {
  customTypeMapping {
    DateTime = "org.threeten.bp.Instant"
  }
  customTypeMapping['URI'] = "okhttp3.HttpUrl"
}

def supportLibraries = configurations.create("supportImplementation")
configurations.all {
  resolutionStrategy.eachDependency { DependencyResolveDetails details ->
    if (details.requested.group == 'com.android.support' &&
        !details.requested.name.contains('multidex')) {
      // We want to force all support libraries to use the same version, even if they're transitive.
      details.useVersion versions.support
    }
    if (details.requested.group == 'com.google.android.gms') {
      // We want to force all play services libraries to use the same version, even if they're transitive.
      details.useVersion versions.playServices
    }
    if (details.requested.group == 'com.google.firebase') {
      // We want to force all play services libraries to use the same version, even if they're transitive.
      details.useVersion versions.firebase
    }
  }
}

dependencies {
  // Support libs
  supportImplementation deps.android.support.annotations
  supportImplementation deps.android.support.appCompat
  supportImplementation deps.android.support.v4
  supportImplementation deps.android.support.design
  supportImplementation deps.android.support.customTabs
  supportImplementation deps.android.support.constraintLayout
  supportImplementation deps.android.support.palette
  supportImplementation deps.android.support.compat

  implementation supportLibraries
  testImplementation supportLibraries
  androidTestImplementation supportLibraries

  // Kotlin
  implementation deps.kotlin.stdlib.core
  implementation deps.kotlin.stdlib.jre7

  // Firebase
  implementation deps.android.firebase.core
  implementation deps.android.firebase.config
  implementation deps.android.firebase.database
  kapt deps.auto.value.extensions.firebase.apt.core
  compileOnly deps.auto.value.extensions.firebase.compileOnly.annotation
  implementation deps.auto.value.extensions.firebase.adapter

  // Square/JW
  implementation deps.butterKnife.runtime
  kapt deps.butterKnife.apt
  implementation deps.misc.diskLruCache
  implementation deps.okhttp.core
  implementation deps.misc.okio
  implementation deps.misc.moshi
  implementation deps.retrofit.core
  implementation deps.retrofit.moshi
  implementation deps.retrofit.rxJava2
  implementation(deps.retrofit.xml) {
    exclude module: 'stax'
    exclude module: 'stax-api'
    exclude module: 'xpp3'
  }
  implementation deps.rx.android
  implementation deps.rx.android2
  implementation deps.rx.java
  implementation deps.rx.java2
  implementation deps.rx.java2Interop
  implementation deps.rx.binding.core
  implementation deps.rx.binding.v4
  implementation deps.rx.binding.design
  implementation deps.misc.timber
  implementation deps.misc.threetenabp
  debugImplementation deps.misc.debug.madge
  debugImplementation deps.misc.debug.scalpel
  debugImplementation deps.misc.debug.processPhoenix
  debugImplementation deps.misc.debug.telescope
  implementation deps.okhttp.debug.loggingInterceptor
  debugImplementation deps.retrofit.debug.mock

  releaseImplementation deps.misc.bugsnag

  // Misc
  implementation deps.autoDispose.core
  implementation deps.autoDispose.android
  implementation deps.autoDispose.kotlin
  errorprone deps.errorProne.build.core
  compileOnly deps.errorProne.compileOnly.annotations
  implementation deps.barber.api
  kapt deps.barber.apt
  implementation deps.glide.core
  implementation deps.glide.okhttp
  implementation deps.misc.lottie
  implementation deps.misc.recyclerViewAnimators
  implementation deps.rx.java
  implementation deps.rx.preferences
  implementation deps.rx.receivers
  implementation deps.rx.relay
  implementation(deps.misc.simpleXml) {
    exclude module: 'stax'
    exclude module: 'stax-api'
    exclude module: 'xpp3'
  }
  implementation deps.misc.moshiLazyAdapters

  // Apollo
  implementation deps.apollo.runtime
  implementation deps.apollo.rx2Support
  implementation deps.apollo.androidSupport

  // Stetho
  debugImplementation deps.stetho.debug.core
  debugImplementation deps.stetho.debug.okhttp

  // Dagger
  kapt deps.dagger.apt.compiler
  compileOnly deps.misc.javaxInject
  implementation deps.dagger.core
  implementation deps.dagger.android

  // AutoValue
  compileOnly deps.auto.value.extensions.moshi.compileOnly.annotations
  kapt deps.auto.value.apt.core
  kapt deps.auto.value.extensions.moshi.apt.core
  kapt deps.auto.value.extensions.redacted.apt.core

  // Conductor
  implementation deps.conductor.core
  implementation deps.conductor.autoDispose
  implementation deps.conductor.support

  implementation deps.misc.jsr305
  implementation deps.misc.unbescape

  // Test
  testImplementation deps.rx.java2Extensions
  testImplementation deps.rx.relay
  androidTestCompileOnly deps.errorProne.compileOnly.annotations
  androidTestImplementation deps.rx.java2
  androidTestImplementation deps.misc.jsr305
  testImplementation deps.misc.jsr305
  testCompileOnly deps.errorProne.compileOnly.annotations
  testImplementation deps.test.junit
  testImplementation deps.test.truth

  // LeakCanary
  debugImplementation deps.leakCanary.debug
  releaseImplementation deps.leakCanary.release

  // Chuck
  debugImplementation deps.chuck.debug
  releaseImplementation deps.chuck.release
}
